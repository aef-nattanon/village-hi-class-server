//===== rAthena Script =======================================
//= Card Removal NPC
//===== By: ==================================================
//= TyrNemesis^
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Removes cards from equipped items.
//===== Additional Comments: =================================
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//= 1.2a Added 'disable_items' command. [Euphy]
//============================================================

morocc,166,90,4	script	NPC ถอดการ์ด#eAcustom	78,{

	set .zenycost,200000;    // base cost of the card remover services (in Zeny)
	set .percardcost,25000;  // cost per card of the card remover services (in Zeny)
	set .faildestroy,1;      // should the card remover have a chance of failure that destroys items? (1=yes, 0=no)

	disable_items;
	mes "[NPC ถอดการ์ด]";
	mes "สวัสดีเด็กน้อย ";
	mes "ฉันมีอำนาจที่จะลบการ์ด";
	mes "ที่คุณใส่ไว้ในอุปกรณ์ของคุณได้";
	mes "ความคิดนี้ทำให้คุณพอใจหรือไม่?";
	next;
	switch(select("ใช่แล้ว:คุณคิดค่าใช้จ่ายอะไร?:ไม่ ขอบคุณ")) {
	case 1:
		mes "[NPC ถอดการ์ด]";
		mes "ดีมาก. ฉันควรตรวจสอบรายการใดให้คุณ?";
		next;

		setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
		for( set .@i,1; .@i <= 10; set .@i,.@i+1 ) {
			if( getequipisequiped(.@indices[.@i]) )
				set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@menu$, .@menu$ + ":";
		}
		set .@part, .@indices[ select(.@menu$) ];
		if(!getequipisequiped(.@part)) {
			mes "[NPC ถอดการ์ด]";
			mes "เจ้าเด็ก... เจ้าไม่ได้สวมสิ่งใดที่นั่นเลยซึ่งข้าสามารถถอดการ์ดออกได้";
			close;
		}
		if(getequipcardcnt(.@part) == 0) {
			mes "[NPC ถอดการ์ด]";
			mes "น้อง... ไม่มีไพ่ประกอบในรายการนี้ ฉันไม่สามารถทำอะไรกับมันได้ ฉันเกรงว่า";
			close;
		}
		set .@cardcount,getequipcardcnt(.@part);
		
		if (!checkweight(1202,(.@cardcount+1))) { 
			mes "^3355FFเดี๋ยวก่อน!";
			mes "ฉันไม่สามารถเสนอสิ่งใดๆ ของฉันได้";
			mes "บริการแก่คุณเพราะว่า";
			mes "คุณแบกของมากเกินไป";
			mes "stuff ใส่ไอเทมพิเศษของคุณเข้าไป";
			mes "คลังเก็บของ Kafra แล้วกลับมาใหม่~";
			close;
		}
		mes "[NPC ถอดการ์ด]";
		mes "รายการนี้มี" + .@cardcount + " การ์ดที่ประกอบอยู่บนนั้น เพื่อที่จะแสดงเวทย์มนตร์ของฉัน ฉันจะต้อง" + (.zenycost+(.@cardcount * .percardcost)) + " zeny, a ^0000FFStar Crumb^000000, and a ^0000FFYellow Gemstone^000000.";
		next;
		if(select("ดีมาก. ทำมัน.:ไม่เป็นไร.") == 2) {
			mes "[NPC ถอดการ์ด]";
			mes "ดีมาก. กลับมาทันทีหากคุณแสวงหาบริการของฉัน";
			close;
		}
		if((zeny < (.zenycost+(.@cardcount * .percardcost))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
			mes "[NPC ถอดการ์ด]";
			mes "คุณไม่มีสิ่งของทั้งหมดที่ฉันต้องการเพื่อใช้เวทย์มนตร์ของฉันนะเด็กน้อย กลับมาอีกครั้งเมื่อคุณทำ";
			close;
		}
		mes "[NPC ถอดการ์ด]";
		mes "ก่อนที่ฉันจะเริ่ม ฉันต้องเตือนคุณก่อน--ฉันอาจจะล้มเหลว ถ้าฉันทำ ฉันอาจทำลายการ์ด ไอเท็ม หรือทั้งสองอย่าง ฉันไม่คืนเงินให้ อย่างที่บอกไปแล้ว อะไรสำคัญกับคุณมากกว่ากัน: ไพ่หรือไอเท็ม?";
		next;
		switch(select("ฉันเปลี่ยนใจเกี่ยวกับเรื่องนี้:รายการ:ไพ่")) {
		case 1:
			mes "[NPC ถอดการ์ด]";
			mes "ดีมาก. กลับมาทันทีหากคุณแสวงหาบริการของฉัน";
			close;
		case 2:
			set .@failtype,1;
			break;
		case 3:
			set .@failtype,2;
		}
		mes "[NPC ถอดการ์ด]";
		mes "ดีมาก. ฉันจะเริ่มต้น.";
		set Zeny, Zeny - (.zenycost+(.@cardcount * .percardcost));
		delitem 1000,1; //Star_Crumb
		delitem 715,1; //Yellow_Gemstone
		
		// Replace the constants in the next 3 lines with failure chance values defined in refine_db.txt
		// First value = Total failure chance (item and cards destroyed)
		// Second value = Partial failure chance (one or the other is destroyed, player decides which one is safe)
		// Third value = Harmless failure chance (all that's lost is your investment)

		set .@failchance,rand(100);
		if (.faildestroy==1) {
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "[NPC ถอดการ์ด]";
				mes "กระบวนการนี้ล้มเหลวโดยสิ้นเชิง ฉันกลัวว่าไอเทมและการ์ดจะถูกทำลาย";
				close;
			}

			if(.@failchance < 8) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;
					mes "[NPC ถอดการ์ด]";
					mes "แม้ว่าฉันจะสามารถเอาการ์ดออกจากไอเท็มได้ แต่การ์ดเหล่านั้นก็ถูกทำลายไปในระหว่างนั้น อย่างไรก็ตามรายการก็โอเค";
					close;
				}

				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "[NPC ถอดการ์ด]";
					mes "น่าเสียดายที่สุด. ฉันถอดการ์ดออกได้สำเร็จ แต่ไอเทมนั้นถูกทำลายไปในระหว่างนั้น";
					close;
				}
			}
		}

		if(.@failchance < 10) {
			next;
			failedremovecards .@part,3;
			mes "[NPC ถอดการ์ด]";
			mes "ฉันไม่สามารถถอดการ์ดออกได้ อย่างไรก็ตาม โชคดีที่ทั้งไอเทมและการ์ดยังคงไม่เป็นไร";
			close;
		}
		next;
		successremovecards .@part;
		mes "[NPC ถอดการ์ด]";
		mes "กระบวนการนี้ประสบความสำเร็จ นี่คือการ์ดและรายการของคุณ ลา.";
		close;
	case 2:
		mes "[NPC ถอดการ์ด]";
		mes "ฉันเรียกเก็บค่าธรรมเนียมคงที่ของ"+callfunc("F_InsertComma",.zenycost)+" zeny, บวก "+callfunc("F_InsertComma",.percardcost)+" zeny fสำหรับการ์ดแต่ละใบที่ฉันลบออกจากรายการ นอกจากนี้ ฉันจำเป็นต้องมี star crumb และ yellow gemstone เพื่อใช้เวทย์มนตร์ของฉัน";
		close;
	case 3:
		mes "[NPC ถอดการ์ด]";
		mes "ดีมาก. กลับมาทันทีหากคุณแสวงหาบริการของฉัน";
		close;
	}
}
