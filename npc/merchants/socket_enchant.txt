//===== rAthena Script =======================================
//= Episode 11.1 Socket Enchant NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 2.0a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2 Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1 Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed Ontouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 2.0a Added 'disable_items' command. [Euphy]
//============================================================

-	script	Seiyablem#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- เดี๋ยวก่อน !! -";
		mes "- ขณะนี้คุณกำลังถือ -";
		mes "- มีของกับคุณมากเกินไป -";
		mes "- กรุณาลองใหม่อีกครั้ง -";
		mes "- หลังจากคุณลดน้ำหนักลง -";
		close;
	}
	mes "[NPC เจาะรู]";
	mes "ฉันเป็นวิศวกรที่เชี่ยวชาญ";
	mes "ในการเจาะรูให้กับอาวุธและชุดเกราะปกติ";
	mes "ค่าบริการของฉัน วัสดุที่จำเป็น";
	mes "และโอกาสในการประสบความสำเร็จทั้งหมด";
	mes "ขึ้นอยู่กับรายการเฉพาะที่ฉันกำลังทำอยู่";
	next;
	mes "[NPC เจาะรู]";
	mes "การเพิ่มสล็อตอาจดูเหมือนง่าย แต่ก็ซับซ้อนกว่าที่คิดมาก";
	mes "หากคุณสนใจในบริการของฉัน แจ้งให้เราทราบ";
	next;
	switch(select("เจาะรูไปที่อาวุธ:เจาะรูไปที่เกราะ:ข้อมูลการลองช่อง:ยกเลิก"))
	{
		case 1:
			mes "[NPC เจาะรู]";
			mes "คุณต้องการเพิ่มช่องให้กับอาวุธหรือไม่?";
			mes "ฉันจัดหมวดหมู่อาวุธตามความยากในการเพิ่มช่อง คลาส C นั้นง่ายที่สุด และคลาส S นั้นยากที่สุด";
			mes "คลาสไหนที่คุณอยากลอง?";
			next;
			switch(select("คลาส C:คลาส B:คลาส A:คลาส S"))
			{
				case 1:
					mes "[NPC เจาะรู]";
					mes "คลาส C ใช่ไหม?";
					mes "ตอนนี้ฉันสามารถอัพเกรดอุปกรณ์บางอย่างในคลาสนั้นได้เท่านั้น ดังนั้นโปรดเลือกหนึ่งในรายการต่อไปนี้ เพื่อที่เราจะได้ลองเพิ่มช่องเข้าไป";
					next;
					switch(select("Trident:Rope:Violin"))
					{
						case 1: callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2: callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3: callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[NPC เจาะรู]";
					mes "คลาสบี?";
					mes "เอาล่ะ นี่คือหมวดหมู่อุปกรณ์โดยเฉลี่ย";
					mes "โปรดเลือกอาวุธที่คุณต้องการให้ฉันลองเพิ่มช่อง";
					next;
					switch(select("Chain:Gladius:Gakkung Bow:Pike:Haedonggum:Lute:Wire:Waghnakh:Arbalest Bow"))
					{
						case 1: callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2: callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3: callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4: callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5: callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6: callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7: callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8: callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9: callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[NPC เจาะรู]";
					mes "โอ้ คลาสเอ";
					mes "นี่คือดินแดนที่มีความเสี่ยงสูง!";
					mes "เอาล่ะ อาวุธไหนที่คุณอยากให้ฉันลองเพิ่มช่อง?";
					next;
					switch(select("Hunter Bow:Survivor's Rod(INT):Zweihander:Flamberge:Infiltrator:Ballista:Stunner:Berserk:Claymore"))
					{
						case 1: callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2: callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3: callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4: callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5: callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6: callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7: callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8: callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9: callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[NPC เจาะรู]";
					mes "วะ วะ วะ วะ...";
					mes "เอสคลาสเหรอ? ใช้ได้...";
					mes "คงจะเป็นเรื่องมหัศจรรย์มากหากฉันสามารถดึงสิ่งนี้ออกมาได้จริง";
					mes "โอเค... เราจะลองเพิ่ม Slot อาวุธไหนดูล่ะ?";
					next;
					switch(select("Gungnir:Poison Knife:Ice Pick:Sucsamad:Ginnungagap:Cutlas:Crescent Scythe:Survivor's Rod(DEX)"))
					{
						case 1: callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4: callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8: callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[NPC เจาะรู]";
			mes "เกราะคุณพูด?";
			mes "แน่นอนไม่มีปัญหา.";
			mes "ชุดเกราะแบ่งออกเป็น 4 คลาสที่แตกต่างกัน ขึ้นอยู่กับความยากในการทำงาน";
			mes "คลาส C นั้นง่ายที่สุด และคลาส S นั้นเป็นคลาสที่ยากที่สุด";
			mes "คลาสไหนที่คุณอยากลอง?";
			next;
			switch(select("C Class:B Class:A Class:S Class"))
			{
				case 1:
					mes "[NPC เจาะรู]";
					mes "เกราะคลาส C ใช่ไหม?";
					mes "ฟังดูสมเหตุสมผล";
					mes "เกราะใดที่คุณต้องการลองเพิ่มช่อง?";
					next;
					switch(select("Mantle:Coat:Circlet:Biretta"))
					{
						case 1: callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2: callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3: callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4: callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[NPC เจาะรู]";
					mes "คุณได้เลือกคลาส B โดยเฉลี่ย";
					mes "แล้วคุณมีเกราะแบบไหนล่ะ?";
					next;
					switch(select("Mirror Shield:Chain Mail:Saint Robe:Silk Robe:Boots:Shoes:Muffler:Guard:Buckler:Shield:Bongun Hat"))
					{
						case 1:  callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2:  callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3:  callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4:  callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5:  callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6:  callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7:  callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8:  callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9:  callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10: callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11: callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[NPC เจาะรู]";
					mes "โอ้ คลาสเอ";
					mes "นี่คือดินแดนที่มีความเสี่ยงสูง!";
					mes "เอาล่ะ คุณอยากให้ฉันลองเพิ่มชุดเกราะไหน";
					next;
					switch(select("Gemmed Sallet:Bucket Hat:Memory Book:Tights:Legion Plate Armor:Full Plate:Thief Clothes:Greaves:Coif:Manteau:Helm:Ninja Suit:Orc Helm:Ancient Cape:Monk Hat:Golden Gear:Brooch:Munak Hat"))
					{
						case 1:  callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2:  callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3:  callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4:  callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5:  callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6:  callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7:  callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8:  callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9:  callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10: callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11: callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12: callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13: callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14: callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15: callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16: callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17: callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18: callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[NPC เจาะรู]";
					mes "เอ่อ... คลาส S?";
					mes "คุณแน่ใจเกี่ยวกับเรื่องนี้?";
					mes "เอาล่ะ... บอกให้ฉันรู้ถึงชุดเกราะที่คุณต้องการเพิ่มช่อง แล้วฉันจะบอกว่าฉันต้องลองทำอะไร";
					next;
					switch(select("Majestic Goat:Spiky Band:Bone Helm:Corsair:Crown:Tiara:Sphinx Hat:Robe of Cast:Earring:Ring:Bow Thimble"))
					{
						case 1:  callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2:  callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3:  callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4:  callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5:  callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6:  callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7:  callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8:  callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9:  callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10: callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11: callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[NPC เจาะรู]";
			mes "เอาล่ะ ฉันยังไม่ได้ขัดเกลาศิลปะของ Slot Addition เลย";
			mes " มันซับซ้อนมากจนฉันต้องโกหกถ้าฉันบอกคุณว่าฉันรู้ทุกปัจจัยที่ส่งผลต่อกระบวนการนี้";
			mes "ถึงกระนั้น ฉันสังเกตเห็นแนวโน้มบางอย่าง...";
			next;
			mes "[NPC เจาะรู]";
			mes " ในบางเมือง การเพิ่มช่องจะประสบความสำเร็จมากกว่าสำหรับอุปกรณ์ที่มีการอัพเกรดน้อยกว่า";
			mes "ในเมืองอื่น สิ่งที่ตรงกันข้ามคือความจริง";
			mes "นั่นไม่แปลกจริงๆเหรอ?";
			next;
			mes "[NPC เจาะรู]";
			mes "โอ้ มีบางสิ่งที่สำคัญจริงๆ ที่คุณควรรู้";
			mes "ถ้าคุณขอให้ฉันเพิ่มช่องให้กับบางสิ่งบางอย่าง ตรวจสอบให้แน่ใจว่าคุณไม่มีช่องนั้นหลายช่องในคลังของคุณ";
			next;
			mes "[NPC เจาะรู]";
			mes "นี่คือตัวอย่าง: ถ้าคุณมี +7 Manteau และ Manteau ปกติในกระเป๋าของคุณ ฉันจะสุ่มเลือกหนึ่งในนั้นสำหรับการเพิ่มช่อง";
			mes "เพียงจำไว้ว่าให้ระวัง";
			next;
			mes "[NPC เจาะรู]";
			mes "อีกครั้ง ^FF0000พกอุปกรณ์อย่างใดอย่างหนึ่งที่คุณต้องการให้ฉันเพิ่มช่อง^000000";
			mes "อุปกรณ์อื่นๆ ทั้งหมดที่มีชื่อเดียวกัน ควรถูกวางไว้ใน Kafra Storage ของคุณ เข้าใจไหม?";
			close;
		case 4:
			mes "[NPC เจาะรู]";
			mes "ใจเย็นๆ นะ นักผจญภัย";
			mes "ถ้าคุณต้องการที่จะลองเพิ่มช่องให้กับอุปกรณ์ของคุณ กรุณากลับมาและแจ้งให้เราทราบ";
			mes "ซียะ~";
			close;
	}
}

function	script	Func_Socket	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[NPC เจาะรู]";
	mes "คุณต้องการเพิ่มสล็อตให้กับ"+ getitemname(.@item_id) +"?";
	mes "เอาล่ะ โปรดพาฉันมาด้วย ^FF0000"+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +"^000000, "+ (.@item_req[1] != 0 && .@req_amount[1] != 0 ? ("^FF0000"+ .@req_amount[1] +" "+ getitemname(.@item_req[1]) + (.@req_amount[1] > 1 && .@item_req[1] != 999 ? "s":"") + "^000000 ") : "") + "and my ^FF0000" + (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny^000000 ค่าบริการ.";
	mes "อ้อ แล้วอย่าลืมเอาอันนั้นมาด้วยล่ะ "+ getitemname(.@item_id) +"!";
	next;
	mes "[NPC เจาะรู]";
	if (getiteminfo(.@item_id, ITEMINFO_LOCATIONS) & EQP_HAND_R) // weapon
	{
		mes "ฉันสามารถลองเพิ่มช่องได้ตอนนี้ถ้าคุณมีไอเทมที่ต้องการและ zeny";
		mes "อย่างไรก็ตาม คุณควรรู้ว่ามีโอกาสที่ฉันจะล้มเหลว";
		mes "เพราะฉะนั้น ฉันจำเป็นต้องเตือนคุณอย่างยุติธรรม...";
		next;
		mes "[NPC เจาะรู]";
		mes "หากความพยายามในการเพิ่มช่องให้กับอาวุธของคุณล้มเหลว ดังนั้น ^FF0000Weapon^000000, ^FF0000และการ์ดใดๆ ที่ประกอบเข้าด้วยกันจะถูกทำลาย^000000";
	}
	else // armor
	{
		mes "ถ้าคุณมีวัตถุดิบที่จำเป็นทั้งหมด ค่าบริการ zeny ของฉัน และชุดเกราะ เราก็สามารถดำเนินการเพิ่ม Slot ต่อไปได้";
  	mes "แต่ก่อนหน้านั้น ฉันต้องเตือนคุณถึงความเสี่ยง";
		next;
		mes "[NPC เจาะรู]";
		mes "หากความพยายามในการเพิ่มช่องให้กับชุดเกราะของคุณล้มเหลว ดังนั้น ^FF0000Armor^000000, ^FF0000 จะเป็นการอัพเกรด^000000 ^FF0000 และการ์ดใดๆ ที่ประกอบเข้าด้วยกันจะถูกทำลาย^000000";
	}
	mes "คุณยังต้องการลองเพิ่มช่องหรือไม่?";
	next;
	switch(select("พยายามเพิ่มช่อง: ยกเลิก"))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[NPC เจาะรู]";
				mes "เอาล่ะ เริ่มงานได้เลย!";
  			mes " คุณควรอธิษฐานขอให้ประสบผลสำเร็จจะดีกว่า";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[NPC เจาะรู]";
					mes "เยี่ยมมาก ดูเหมือนว่าจะประสบความสำเร็จ";
  				mes " มันดูทำได้ดีทีเดียว ยินดีด้วย!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[NPC เจาะรู]";
					mes "แล้วพบกันใหม่นะเพื่อน!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[NPC เจาะรู]";
					mes "ว้าว! ...ฉันขอโทษ มันล้มเหลว";
					mes "อย่างไรก็ตาม ฉันบริสุทธิ์จริงๆ";
					mes "นี่คือโชคของคุณ และมันถูกลิขิตโดยพระเจ้า โอเคไหม?";
					mes "อย่าผิดหวังเลย และพยายามครั้งหน้า";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[NPC เจาะรู]";
					mes " ฉันขอให้คุณโชคดีในครั้งต่อไป!";
					close;
				}
			}
			else
			{
				mes "[NPC เจาะรู]";
				mes " ฉันอยากจะดำเนินการต่อด้วยการเพิ่ม Slot นี้ แต่คุณยังขาดบางสิ่งไป";
  			mes "คุณแน่ใจว่าคุณมีอุปกรณ์, วัสดุที่จำเป็น และ zeny?";
				close;
			}
		case 2:
			mes "[NPC เจาะรู]";
			mes "ต้องการเวลาคิดเกี่ยวกับมันหน่อยมั้ย?";
			mes "เอาล่ะ ฉันเข้าใจแล้ว";
			mes "แค่จำไว้ว่าชีวิตมันไม่สนุกเลย ถ้าคุณใช้ชีวิตอย่างปลอดภัยอยู่เสมอ~";
			close;
	}
}

payon,140,151,5	duplicate(SocketEnchant)	Seiyablem#pay	84
lhz_in02,281,35,5	duplicate(SocketEnchant)	Seiyablem#lhz	84
prt_in,33,70,5	duplicate(SocketEnchant)	Seiyablem#prt	84
morocc,163,90,4	duplicate(SocketEnchant)	NPC เจาะรู#moc	617

-	script	Young Man#dummy::SocketMan	-1,{
	mes "[Young Man]";
	mes "I'm considering becoming a Slotting Engineer.";
	mes "It's a new field where you can add Slots to equipment, and it'd be cool if I can work in such a lucrative career.";
	next;
	mes "[Young Man]";
	mes "There's a huge element of luck involved in successfully adding Slots from what I can understand.";
	mes "You fail sometimes, but if you're super lucky, you can add two Slots.";
	mes "Crazy. Huh?";
	next;
	mes "[Young Man]";
	mes "But don't get too excited.";
	mes "Only certain equipment can handle the addition of two extra Slots.";
	mes "There's so much more I have to learn before I can even be an apprentice...";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	Young Man#prt	97
payon,143,143,7	duplicate(SocketMan)	Young Man#pay	97
morocc,60,42,3	duplicate(SocketMan)	Young Man#moc	97
lhz_in02,269,33,5	duplicate(SocketMan)	Young Man#lhz	97
